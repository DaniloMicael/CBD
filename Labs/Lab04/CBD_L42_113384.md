// NMEC: 113384

# Lab04 - Ex 4.2

## 1. Encontre todos os atores que dirigiram um filme em que também atuaram e apresente o nome do ator e o título do filme.
```sql
MATCH (actor:Person)-[:ACTED_IN]->(movie:Movie)<-[:DIRECTED]-(actor)
RETURN actor.name AS Actor, movie.title as Movie
```

## 2. Para cada filme realizado depois de 2005, apresente os nomes de todos os atores que atuaram nesse filme.
```sql
MATCH (actor:Person)-[:ACTED_IN]->(movie:Movie)
WHERE movie.released > 2005
RETURN movie.title AS Movie, collect(actor.name) AS Actors
```

## 3. Encontre pares de nós com mais do que uma relação entre si.
```sql
MATCH (a)-[r]-(b)
WITH a, b, COUNT(r) as relationCount
WHERE relationCount > 1
RETURN a, b, relationCount
```

## 4. Encontre todos os pares de pessoas que fizeram revisões do mesmo filme. Apresente os seus nomes e título de cada filme.
```sql
MATCH (person1:Person)-[:REVIEWED]->(movie:Movie)<-[:REVIEWED]-(person2:Person)
WHERE person1.name < person2.name
RETURN person1.name As Reviewer1, person2.name AS Reviewer2, movie.title as Movie
```

## 5. Encontre todos os pares de atores que atuaram em vários filmes juntos.
```sql
MATCH (actor1:Person)-[:ACTED_IN]->(movie:Movie)<-[:ACTED_IN]-(actor2:Person)
WITH actor1, actor2, COUNT(movie) AS sharedActedMovies
WHERE sharedActedMovies > 1 AND person1.name < person2.name
RETURN actor1.name AS Actor1, actor2.name AS Actor2, sharedActedMovies
```

## 6. Determine a idade média do elenco do filme "Apollo 13" no ano do lançamento do filme.
```Sql
MATCH (actor:Person)-[:ACTED_IN]->(movie:Movie {title: "Apollo 13"})
RETURN avg(movie.released - actor.born) AS AverageAge
```

## 7. Encontre os 10 filmes com o elenco mais velho no momento do lançamento do filme. Apresente o filme e a idade média arredondada a 2 casas decimais, por ordem decrescente.
```sql
MATCH (actor:Person)-[:ACTED_IN]->(movie:Movie)
WITH movie, avg(movie.released - actor.born) AS AverageAge
RETURN movie.title as Movie, round(AverageAge, 2) 
ORDER BY AverageAge DESC
LIMIT 10
```

## 8. Apresente o subgrafo ACTED_IN do filme com o elenco mais novo, no momento do lançamento do filme.
```sql
MATCH (actor:Person)-[:ACTED_IN]->(movie:Movie)
WITH movie, avg(movie.released - actor.born) AS AverageAge, collect(actor) AS actors
RETURN actors, movie
ORDER BY AverageAge ASC
LIMIT 1
```

## 9. Qual é o caminho mais curto (usando qualquer tipo de relação) entre John Cusack e Demi Moore?
```sql
MATCH p = SHORTEST 1 (person1:Person {name: "John Cusack"})-[*]-(person2:Person {name: "Demi Moore"})
RETURN p
```

**Note:** I could RETURN [n in nodes(p) | coalesce(n.name, n.title) ] to see the path like ["node1", "node2", ...]
- The function coalesce returns the first non-null value in a list of expressions.

## 10. Qual a dimensão caminho mais curto (usando qualquer tipo de relação) entre Keanu Reeves e Tom Cruise?
```sql
MATCH p = SHORTEST 1 (person1:Person {name: "Keanu Reeves"})-[*]-(person2:Person {name: "Tom Cruise"})
RETURN length(p) AS PathLength
```

## 11. Quais são as dimensões do caminho mais curto entre pessoas com nome Jim e pessoas com nome Kevin?
```sql
MATCH p = SHORTEST 1 (person1:Person)-[*]-(person2:Person)
WHERE person1.name STARTS WITH "Jim" AND person2.name STARTS WITH "Kevin"
RETURN length(p) AS PathLength
```

## 12. Que pessoas têm uma distância 2 para Jim Cash (a distância entre duas pessoas é o comprimento do caminho mais curto entre eles)?
```sql
MATCH p = SHORTEST 1 (jim:Person {name: "Jim Cash"})-[*2]-(otherPerson:Person)
RETURN otherPerson.name AS Person, length(p) AS PathLength
```

## 13. Qual é a maior distância de uma pessoa para Kevin Bacon?
```sql
MATCH p = SHORTEST 1 (kevin:Person {name: "Kevin Bacon"})-[*]-(otherPerson:Person)
WHERE kevin <> otherPerson
RETURN max(length(p))
```

## 14. Qual é a maior distância entre duas pessoas?
```sql
MATCH p = SHORTEST 1 (person1:Person)-[*]-(person2:Person)
WHERE person1 <> person2
RETURN max(length(p))
```

## 15. Qual é a distribuição de distâncias em pares (isto é, para a distância 1, 2, 3, ..., quantos pares de pessoas têm essa distância um do outro)?
```sql
MATCH p = SHORTEST 1 (person1:Person)-[*]-(person2:Person)
WHERE person1 <> person2 AND person1.name < person2.name
RETURN length(p) AS Distance, count(*) AS PairCount
ORDER BY Distance
```

## 16. Indique as 10 pessoas com menor distância média em que o caminho entre elas são relações do tipo ACTED_IN.
```sql
MATCH p = SHORTEST 1 (person1:Person)-[r:ACTED_IN*]-(person2:Person)
WHERE person1 <> person2 AND person1.name < person2.name
WITH person1, avg(length(p)) AS AvgDistance
RETURN person1.name AS Person1, round(AvgDistance, 2)
ORDER BY AvgDistance ASC
LIMIT 10
```

## 17. Apresente os atores que tiveram pelo menos uma role para personagens com o prefixo 'Dr.'. O resultado deve ser uma lista com o par “Ator, Lista das roles”. (roles que comecam por Dr, nao sao todas)
```sql
MATCH (actor:Person)-[r:ACTED_IN]->(movie:Movie)
WITH actor, [role IN r.roles WHERE role STARTS WITH "Dr."] AS filteredRoles
WHERE size([role IN r.roles WHERE role STARTS WITH "Dr."]) > 0
UNWIND filteredRoles AS roles
WITH actor.name AS Actor, collect(roles) as Roles
RETURN [Actor, Roles] AS ActorRoles
```
- pensar melhor nesta

## 18. Encontre as 3 pessoas que tiveram mais roles (diferentes) ao longo da sua carreira. Apresente o nome da pessoa, os roles e o número de roles.
```sql
MATCH (actor:Person)-[r:ACTED_IN]->(movie:Movie)
WITH actor, count(DISTINCT r.roles) AS roleCount, collect(DISTINCT r.roles) AS roles
RETURN actor.name AS Actor, roles, roleCount
ORDER BY roleCount DESC
LIMIT 3
```

## 19. Qual foi a maior diferença de idade entre dois atores que atuaram no mesmo filme?
```sql
MATCH (actor1:Person)-[:ACTED_IN]->(movie:Movie)<-[:ACTED_IN]-(actor2:Person)
WHERE actor1 <> actor2
WITH movie, abs(actor1.born - actor2.born) AS ageDiff
RETURN movie.title AS Movie, max(ageDiff) AS MaxAgeDiff
ORDER BY MaxAgeDiff DESC
LIMIT 1
```

## 20. Quais são os 5 atores com maior número de colaborações (filmes em que ambos atuaram)?
```sql
MATCH (actor1:Person)-[:ACTED_IN]->(movie:Movie)<-[:ACTED_IN]-(actor2:Person)
WHERE actor1 <> actor2
WITH actor1, actor2, count(movie) AS sharedMovies
RETURN actor1.name AS Actor1, actor2.name AS Actor2, sharedMovies
ORDER BY sharedMovies DESC
LIMIT 5
```